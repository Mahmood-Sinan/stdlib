#:include "common.fypp"
#:set RANKS = range(1, 2+1)
#:set R_KINDS_TYPES = list(zip(REAL_KINDS, REAL_TYPES, REAL_SUFFIX))
#:set C_KINDS_TYPES = list(zip(CMPLX_KINDS, CMPLX_TYPES, CMPLX_SUFFIX))
#:set KINDS_TYPES = R_KINDS_TYPES+C_KINDS_TYPES
submodule (stdlib_specialmatrices) tridiagonal_matrices
    use stdlib_linalg_lapack, only: lagtm, gttrf, gttrs

    character(len=*), parameter :: this = "tridiagonal matrices"
    contains

    !--------------------------------
    !-----                      -----
    !-----     CONSTRUCTORS     -----
    !-----                      -----
    !--------------------------------

    #:for k1, t1, s1 in (KINDS_TYPES)
    pure module function initialize_tridiagonal_pure_${s1}$(dl, dv, du) result(A)
        !! Construct a `tridiagonal` matrix from the rank-1 arrays
        !! `dl`, `dv` and `du`.
        ${t1}$, intent(in) :: dl(:), dv(:), du(:)
        !! tridiagonal matrix elements.
        type(tridiagonal_${s1}$_type) :: A
        !! Corresponding tridiagonal matrix.

        ! Internal variables.
        integer(ilp) :: n
        type(linalg_state_type) :: err0

        ! Sanity check.
        n = size(dv, kind=ilp)
        if (n <= 0) then
            err0 = linalg_state_type(this, LINALG_VALUE_ERROR, "Matrix size needs to be positive, n = ", n, ".")
            call linalg_error_handling(err0)
        endif
        if (size(dl, kind=ilp) /= n-1) then
            err0 = linalg_state_type(this, LINALG_VALUE_ERROR, "Vector dl does not have the correct length.")
            call linalg_error_handling(err0)
        endif
        if (size(du, kind=ilp) /= n-1) then
            err0 = linalg_state_type(this, LINALG_VALUE_ERROR, "Vector du does not have the correct length.")
            call linalg_error_handling(err0)
        endif

        ! Description of the matrix.
        A%n = n
        ! Matrix elements.
        A%dl = dl ; A%dv = dv ; A%du = du
    end function

    pure module function initialize_constant_tridiagonal_pure_${s1}$(dl, dv, du, n) result(A)
        !! Construct a `tridiagonal` matrix with constant elements.
        ${t1}$, intent(in) :: dl, dv, du
        !! tridiagonal matrix elements.
        integer(ilp), intent(in) :: n
        !! Matrix dimension.
        type(tridiagonal_${s1}$_type) :: A
        !! Corresponding tridiagonal matrix.

        ! Internal variables.
        integer(ilp) :: i
        type(linalg_state_type) :: err0

        ! Description of the matrix.
        A%n = n
        if (n <= 0) then
            err0 = linalg_state_type(this, LINALG_VALUE_ERROR, "Matrix size needs to be positive, n = ", n, ".")
            call linalg_error_handling(err0)
        endif
        ! Matrix elements.
        allocate( A%dl(n-1), source = dl )
        allocate( A%dv(n), source= dv )
        allocate( A%du(n-1), source = du )
    end function

    module function initialize_tridiagonal_impure_${s1}$(dl, dv, du, err) result(A)
        !! Construct a `tridiagonal` matrix from the rank-1 arrays
        !! `dl`, `dv` and `du`.
        ${t1}$, intent(in) :: dl(:), dv(:), du(:)
        !! tridiagonal matrix elements.
        type(linalg_state_type), intent(out) :: err
        !! Error handling.
        type(tridiagonal_${s1}$_type) :: A
        !! Corresponding tridiagonal matrix.

        ! Internal variables.
        integer(ilp) :: n
        type(linalg_state_type) :: err0

        ! Sanity check.
        n = size(dv, kind=ilp)
        if (n <= 0) then
            err0 = linalg_state_type(this, LINALG_VALUE_ERROR, "Matrix size needs to be positive, n = ", n, ".")
            call linalg_error_handling(err0, err)
        endif
        if (size(dl, kind=ilp) /= n-1) then
            err0 = linalg_state_type(this, LINALG_VALUE_ERROR, "Vector dl does not have the correct length.")
            call linalg_error_handling(err0, err)
        endif
        if (size(du, kind=ilp) /= n-1) then
            err0 = linalg_state_type(this, LINALG_VALUE_ERROR, "Vector du does not have the correct length.")
            call linalg_error_handling(err0, err)
        endif

        if(err0%ok()) then
            ! Description of the matrix.
            A%n = n
            ! Matrix elements.
            A%dl = dl ; A%dv = dv ; A%du = du
        endif
    end function

    module function initialize_constant_tridiagonal_impure_${s1}$(dl, dv, du, n, err) result(A)
        !! Construct a `tridiagonal` matrix with constant elements.
        ${t1}$, intent(in) :: dl, dv, du
        !! tridiagonal matrix elements.
        integer(ilp), intent(in) :: n
        !! Matrix dimension.
        type(linalg_state_type), intent(out) :: err
        !! Error handling
        type(tridiagonal_${s1}$_type) :: A
        !! Corresponding tridiagonal matrix.

        ! Internal variables.
        integer(ilp) :: i
        type(linalg_state_type) :: err0

        if (n <= 0) then
            err0 = linalg_state_type(this, LINALG_VALUE_ERROR, "Matrix size needs to be positive, n = ", n, ".")
            call linalg_error_handling(err0, err)
        endif

        if(err0%ok()) then
            ! Description of the matrix.
            A%n = n
            ! Matrix elements.
            allocate( A%dl(n-1), source = dl )
            allocate( A%dv(n), source= dv )
            allocate( A%du(n-1), source = du )
        endif
    end function
    #:endfor

    !-----------------------------------------
    !-----                               -----
    !-----     MATRIX-VECTOR PRODUCT     -----
    !-----                               -----
    !-----------------------------------------

    !! spmv_tridiag
    #:for k1, t1, s1 in (KINDS_TYPES)
    #:for rank in RANKS
    module subroutine spmv_tridiag_${rank}$d_${s1}$(A, x, y, alpha, beta, op)
        type(tridiagonal_${s1}$_type), intent(in) :: A
        ${t1}$, intent(in), contiguous, target :: x${ranksuffix(rank)}$
        ${t1}$, intent(inout), contiguous, target :: y${ranksuffix(rank)}$
        real(${k1}$), intent(in), optional :: alpha
        real(${k1}$), intent(in), optional :: beta
        character(1), intent(in), optional :: op

        ! Internal variables.
        real(${k1}$) :: alpha_, beta_
        integer(ilp) :: n, nrhs, ldx, ldy
        character(1) :: op_
        #:if rank == 1
        ${t1}$, pointer :: xmat(:, :), ymat(:, :)
        #:endif

        ! Deal with optional arguments.
        alpha_ = 1.0_${k1}$ ; if (present(alpha)) alpha_ = alpha
        beta_  = 0.0_${k1}$ ; if (present(beta))  beta_  = beta
        op_    = "N"        ; if (present(op))    op_    = op

        ! Prepare Lapack arguments.
        n = A%n ; ldx = n ; ldy = n ;
        nrhs = #{if rank==1}# 1 #{else}# size(x, dim=2, kind=ilp) #{endif}#

        #:if rank == 1
        ! Pointer trick.
        xmat(1:n, 1:nrhs) => x ; ymat(1:n, 1:nrhs) => y
        call lagtm(op_, n, nrhs, alpha_, A%dl, A%dv, A%du, xmat, ldx, beta_, ymat, ldy)
        #:else
        call lagtm(op_, n, nrhs, alpha_, A%dl, A%dv, A%du, x, ldx, beta_, y, ldy)
        #:endif
    end subroutine
    #:endfor
    #:endfor

    #:for k1, t1, s1 in (KINDS_TYPES)
    module subroutine solve_tridiag_${s1}$(A, b, x)
        type(tridiagonal_${s1}$_type), intent(in) :: A
        ${t1}$, intent(in), contiguous :: b(:,:)
        ${t1}$, intent(inout), contiguous, target :: x(:,:)

        !Internal variables.
        ${t1}$, allocatable :: dl(:), du(:), dv(:), du2(:)
        integer(ilp) :: n, nrhs, info
        integer(ilp), allocatable :: ipiv(:)

        n = A%n
        nrhs = size(b,2)
        x = b
        allocate(dv(n), source = A%dv)
        if(n > 1) then
            allocate(dl(n - 1), source = A%dl)
            allocate(du(n - 1), source = A%du)
        end if
        if(n > 2) then
            allocate(du2(n - 2))
            du2 = 0.0_${k1}$
        end if
        allocate(ipiv(n))
        call gttrf(n, dl, dv, du, du2, ipiv, info)
        call gttrs('N', n, nrhs, dl, dv, du, du2, ipiv, x, n, info)
    end subroutine
    #:endfor

    !-------------------------------------
    !-----                           -----
    !-----     UTILITY FUNCTIONS     -----
    !-----                           -----
    !-------------------------------------

    #:for k1, t1, s1 in (KINDS_TYPES)
    pure module function tridiagonal_to_dense_${s1}$(A) result(B)
        !! Convert a `tridiagonal` matrix to its dense representation.
        type(tridiagonal_${s1}$_type), intent(in) :: A
        !! Input tridiagonal matrix.
        ${t1}$, allocatable :: B(:, :)
        !! Corresponding dense matrix.

        ! Internal variables.
        integer(ilp) :: i

        associate (n => A%n)
        #:if t1.startswith('complex')
        allocate(B(n, n), source=zero_c${k1}$)
        #:else
        allocate(B(n, n), source=zero_${k1}$)
        #:endif
        B(1, 1) = A%dv(1) ; B(1, 2) = A%du(1)
        do concurrent (i=2:n-1)
            B(i, i-1) = A%dl(i-1)
            B(i, i) = A%dv(i)
            B(i, i+1) = A%du(i)
        enddo
        B(n, n-1) = A%dl(n-1) ; B(n, n) = A%dv(n)
        end associate
    end function
    #:endfor

    #:for k1, t1, s1 in (KINDS_TYPES)
    pure module function transpose_tridiagonal_${s1}$(A) result(B)
        type(tridiagonal_${s1}$_type), intent(in) :: A
        !! Input matrix.
        type(tridiagonal_${s1}$_type) :: B
        B = tridiagonal(A%du, A%dv, A%dl)
    end function
    #:endfor

    #:for k1, t1, s1 in (KINDS_TYPES)
    pure module function hermitian_tridiagonal_${s1}$(A) result(B)
        type(tridiagonal_${s1}$_type), intent(in) :: A
        !! Input matrix.
        type(tridiagonal_${s1}$_type) :: B
        #:if t1.startswith("complex")
        B = tridiagonal(conjg(A%du), conjg(A%dv), conjg(A%dl))
        #:else
        B = tridiagonal(A%du, A%dv, A%dl)
        #:endif
    end function
    #:endfor

    #:for k1, t1, s1 in (KINDS_TYPES)
    module function determinant_tridiagonal_${s1}$(A) result(B)
        type(tridiagonal_${s1}$_type), intent(in) :: A
        ${t1}$ :: B

        integer(ilp) :: i
        ${t1}$ :: f0, f1, f2
        #:if t1.startswith("complex")
            f0 = one_c${k1}$
        #:else
            f0 = one_${k1}$
        #:endif
        associate(du => A%du, dv => A%dv, dl => A%dl, n => A%n)
            f1 = dv(1)
            if(n == 1) then
                B = f1
                return
            end if

            do i = 2, n
                f2 = dv(i)*f1 - dl(i - 1) * du(i - 1) * f0
                f0 = f1
                f1 = f2
            end do
            B = f2
        end associate
    end function
    #:endfor

    #:for k1, t1, s1 in (KINDS_TYPES)
    module function inverse_tridiagonal_${s1}$(A) result(B)
        type(tridiagonal_${s1}$_type), intent(in) :: A
        ${t1}$, allocatable :: B(:, :)
        ${t1}$, allocatable :: dl(:), du(:), dv(:), du2(:)
        integer(ilp) :: n, info
        integer(ilp), allocatable :: ipiv(:)

        n = A%n
        allocate(B(n,n))
        B = 0.0_${k1}$
        do info = 1, n
            B(info, info) = 1.0_${k1}$
        end do
        allocate(dv(n), source = A%dv)
        if(n > 1) then
            allocate(dl(n - 1), source = A%dl)
            allocate(du(n - 1), source = A%du)
        end if
        if(n > 2) then
            allocate(du2(n - 2))
            du2 = 0.0_${k1}$
        end if
        allocate(ipiv(n))
        call gttrf(n, dl, dv, du, du2, ipiv, info)
        if (info /= 0) then
            print *, "inverse_tridiagonal: matrix is singular at pivot ", info
            B = 0.0_${k1}$
            return
        end if
        call gttrs('N', n, n, dl, dv, du, du2, ipiv, B, n, info)
    end function
    #:endfor

    #:for k1, t1, s1 in (KINDS_TYPES)
    pure module function scalar_multiplication_tridiagonal_${s1}$(alpha, A) result(B)
        ${t1}$, intent(in) :: alpha
        type(tridiagonal_${s1}$_type), intent(in) :: A
        type(tridiagonal_${s1}$_type) :: B
        B = tridiagonal(A%dl, A%dv, A%du)
        B%dl = alpha*B%dl; B%dv = alpha*B%dv; B%du = alpha*B%du
    end function

    pure module function scalar_multiplication_bis_tridiagonal_${s1}$(A, alpha) result(B)
        type(tridiagonal_${s1}$_type), intent(in) :: A
        ${t1}$, intent(in) :: alpha
        type(tridiagonal_${s1}$_type) :: B
        B = tridiagonal(A%dl, A%dv, A%du)
        B%dl = alpha*B%dl; B%dv = alpha*B%dv; B%du = alpha*B%du
    end function
    #:endfor

    #:for k1, t1, s1 in (KINDS_TYPES)
    pure module function matrix_add_tridiagonal_${s1}$(A, B) result(C)
        type(tridiagonal_${s1}$_type), intent(in) :: A
        type(tridiagonal_${s1}$_type), intent(in) :: B
        type(tridiagonal_${s1}$_type) :: C
        C = tridiagonal(A%dl, A%dv, A%du)
        C%dl = C%dl + B%dl; C%dv = C%dv + B%dv; C%du = C%du + B%du
    end function

    pure module function matrix_sub_tridiagonal_${s1}$(A, B) result(C)
        type(tridiagonal_${s1}$_type), intent(in) :: A
        type(tridiagonal_${s1}$_type), intent(in) :: B
        type(tridiagonal_${s1}$_type) :: C
        C = tridiagonal(A%dl, A%dv, A%du)
        C%dl = C%dl - B%dl; C%dv = C%dv - B%dv; C%du = C%du - B%du
    end function
    #:endfor

end submodule
